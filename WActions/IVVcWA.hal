remote inner procedure IVSumup(var record IVVc,Boolean);
remote function Boolean okedIVCashVc_PasteCustCode(var record IVCashVc,string,string,string,var Integer,var string);

global
function Boolean IVDClassCalcProfitVATButtonAction(Integer wn,Integer value)
begin
  Boolean res;
  record IVVc IVr;

  res = true;
  GetWindowRecord(wn,IVr);
  if IVr.OKFlag==1 then begin
    res=false;
  end;
  
  IVDClassCalcProfitVATButtonAction = res;
  return;
End;

global
function Boolean IVDClassCalcProfitVATButtonAfter(Integer wn,Boolean changedf)
begin
  record IVVc IVr;
  Boolean res;

  res=true;
  
  if changedf then begin
    GetWindowRecord(wn,IVr);
    IVSumup(IVr,true);
    PutWindowRecord(wn,IVr);
  end;
  
  IVDClassCalcProfitVATButtonAfter = res;
  return;
end; 

global
function Boolean EBS_ConnectPrepayInvVClassOnOKWindow(Integer wn)
begin
  Boolean res;
  record RcVc RepSpec;

  res = true;
  
  DeselectWindow(wn,true);
  GetWindowRecord(wn,RepSpec);
  if (blank(RepSpec.f2)) then begin
    MessageBox(1058,"");
    WindowFieldGoto(wn,RepSpec,-1,"f2",0);
    res = false;
    goto LEBS_ConnectPrepayInvVClassOnOKWindow;
  end;
  if (blank(RepSpec.d1)) then begin
    MessageBox(1058,"");
    WindowFieldGoto(wn,RepSpec,-1,"d1",0);
    res = false;
    goto LEBS_ConnectPrepayInvVClassOnOKWindow;
  end;

LEBS_ConnectPrepayInvVClassOnOKWindow:;
  SelectWindow(wn);
  EBS_ConnectPrepayInvVClassOnOKWindow = res;
  return;
end;

global
procedure EBS_ConnectPrepayInvVClassReportDefaults(Integer wn)
begin
  record RcVc RepSpec;

  DeselectWindow(wn,false);
  GetWindowRecord(wn,RepSpec);
  ReportDefaults(RepSpec,"EBS_ConnectPrepayInvVClass");
  RepSpec.flags[3] = 1;
  PutWindowRecord(wn,RepSpec);
  SelectWindow(wn);
  return;
end;

/*
global function integer EmailValidationStatusWithStdID()
begin		
	EmailValidationStatusWithStdID = 2;
end;
*/



global
function Boolean IVCashDClassActiveEditField(Integer wn,string fieldname,Integer fn,Integer wnst,Integer rownr,Integer changed)
BEGIN
  boolean res;

  res =  inner.IVCashDClassActiveEditField(wn,fieldname,fn,wnst,rownr,changed); 
  
  if(fieldname=="CustCode")then begin
    if(usercanaction("AllowChangeCustInIVCash",false))then begin
      res = true;
    end;  
  end;
  
  IVCashDClassActiveEditField = res;
  RETURN;
END;


function Boolean okedIVCashDClassCustCodeEFAfter(Integer wn,Integer rownr,Integer changed)
begin
  record IVCashVc IVCashr;
  Integer err;
  string 255 warning;
  record LocalMachineBlock LMb;
  
  GetWindowRecord(wn,IVCashr);
  if ((changed!=0) or (blank(IVCashr.Addr0) and (nonblank(IVCashr.CustCode)) )) then begin
    BlockLoad(LMb);
    if (okedIVCashVc_PasteCustCode(IVCashr,WindEFstr(wn),LMb.DefLocation,LMb.PLCode,err,warning)) then begin
      if (nonblank(warning)) then begin
        MessageBox(0,warning);
      end;
      if (err!=0) then begin
        MessageBox(err,"");
      end;
    end else begin
      if (err!=0) then begin
        MessageBox(err,"");
      end;
    end;
    PutWindowRecord(wn,IVCashr);
  end;
  okedIVCashDClassCustCodeEFAfter = true;
  return;
end;

global
function Boolean IVCashDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  boolean res;
  record IVCashVc IVCashr;
  
  
  getwindowrecord(wn,IVCashr);
  if(IVCashr.OKFlag==1 and fieldname=="CustCode")then begin
    okedIVCashDClassCustCodeEFAfter(wn,rownr,changed)
  end else begin
    res =  inner.IVCashDClassAfterEditField(wn,fieldname,fn,rownr,changed); 
  end;
  
  IVCashDClassAfterEditField = res;
  RETURN;
END;