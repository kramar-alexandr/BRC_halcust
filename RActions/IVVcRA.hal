external function boolean OBS_InvHasNoBasePrice(record IVVc,var integer);

global
updating function LongInt IVVcRecordCheck(record IVVc IVp,record IVVc IV2p,LongInt stat,LongInt long4)
begin
  LongInt res;
  record IVVc cIVr;
  integer rw;
  boolean transf;

  if ((IVp.OKFlag==1) or (IVp.OKFlag==6)) then begin
    if (stat==Rs_insert) then begin transf = true; end;
    if (stat==Rs_update) then begin
      if (IV2p.OKFlag==0) then begin transf = true; end;
    end;
  end;

  if transf and IVp.CredManNr != -1 then begin
    cIVr.CredManSourceInv = IVp.SerNr;
    if ReadFirstKey("CredManSourceInv",cIVr,1,true) then begin
      RecordCheckError(1000004," " & cIVr.SerNr,-1,"SerNr");
      res = -1;
      goto LIVVcRecordCheck;
    end;
  end;

  if IVp.CalcProfitVAT==1 and transf then begin
    if OBS_InvHasNoBasePrice(IVp,rw) then begin
      RecordCheckError(1001002,"",rw,"BasePrice");
      res = -1;
      goto LIVVcRecordCheck;
    end;
  end;
  
  res = inner.IVVcRecordCheck(IVp,IV2p,stat,long4);

LIVVcRecordCheck:;
  IVVcRecordCheck = res;
  return;
end;

global
function LongInt IVVcRecordDuplicate(var record IVVc IVr,record IVVc IV2r,LongInt long3,LongInt long4)
begin
  LongInt res;

  res = inner.IVVcRecordDuplicate(IVr,IV2r,long3,long4);
  IVr.CredManSourceInv = -1;

  IVVcRecordDuplicate = res;
  RETURN;
END;
